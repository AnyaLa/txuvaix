Alphabet
  а ӑ е ё ӗ и о у ӳ ы э ю я б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ
  А Ӑ Е Ё Ӗ И О У Ӳ Ы Э Ю Я Б В Г Д Ж З К Л М Н П Р С Ҫ Т Ф Х Ц Ч Ш Щ Й Ь Ъ 
 %{A%}:а %{A%}:е
 %{Ă%}:ӑ %{Ă%}:ӗ %{Ă%}:0
 %{ă%}:ӑ %{ă%}:ӗ
 %{T%}:т %{T%}:р %{T%}:ч
 %{и%}:и %{и%}:ӗ %{и%}:0 
 %{м%}:м %{м%}:0
 %{н%}:н %{н%}:0
 %{N%}:н %{N%}:0
 %{й%}:й %{й%}:0 %{й%}:ь 
 %{у%}:ӳ %{у%}:у
 %{ː%}:0 %{ː%}:н
 %{в%}:в %{в%}:0
 %{ъ%}:0 
 %{ь%}:0

 %-

 %>:0 
;

Sets

ArchiBack = %{ъ%} ; 

ArchiFront = %{ь%} ; 

BackVow = ӑ а ы о у я ё ю ;

FrontVow = ӗ э и ӳ е ; 

ForceHarmony = %{ъ%} %{ь%} ;

Vow = ӑ а ы о у я ё ю ӗ э и ӳ е ;

ArchiCns = %{м%} %{н%} %{T%} %{й%} %{ː%} %{N%} %{в%} ;

Cns = б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ ; 

Rules 

"Back vowel harmony for archiphoneme {A}"
%{A%}:а <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :BackVow ]+ _ ; 
       except
            [ н | л ] ь: %>: ( :0) _ ;

!% п а л ь т о > {N} {A}
!% п а л ь т о 0  н   а

"Front vowel harmony for archiphoneme {A}"
%{A%}:е <=> [ ArchiFront: | FrontVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :FrontVow ]+ _ ; 
            [ н | л ] ь: %>: ( :0) _ ;
        except 
            [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :BackVow ]+ _ ;

!% и ю л ь > {N} {A}
!% и ю л 0 0  0   е

"Back vowel harmony for archiphoneme {Ă}"
%{Ă%}:ӑ <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :BackVow ]+ _ ;
      except
            %{м%}: %>: _ н ; 
                :ӑ %>: _ ; 
                 о %>: _ н ;
    %{у%}:у %{н%}: %>: _ н ; 
      :Vow [ :0 ]* %>: _ н ;



"Back vowel harmony for archiphoneme {у}"
%{у%}:у <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ]  | :BackVow ]+ _ ;
      except
           ArchiFront: %>: _ ;

"Front vowel harmony for archiphoneme {у}"
%{у%}:ӳ <= ArchiFront: %>: _ ;

!% п а л ь т о > {ă} р > ш {Ă} н
!% п а л ь т 0 0  ӑ  р 0 ш  ӑ  н

"Non surface {Ă} in plural genitive"
%{Ă%}:0 <=> %{м%}: %>: _ н ; 
                :ӑ %>: _ ; 
                 о %>: _ н ;
    %{у%}:у %{н%}: %>: _ н ; 
      :Vow [ :0 ]* %>: _ н ;

!% п а л ь т о > {Ă} н
!% п а л ь т о 0  0  н

!% п а л ь т о > {и} {н} > {Ă} н
!% п а л ь т 0 0  и   0  0  0  н

!% п а л ь т о > {у} {н} > {Ă} н
!% п а л ь т 0 0  у   0  0  0  н

"Realisation of {м}"
%{м%}:м <=> _ .#. ;           ! Nominative
            _ %>: [ п | ш ] ; ! Instrumental, Terminative

"Realisation of {м} with certain obliques"
%{м%}:н <=> _ %>: %{T%}: ; 

"Realisation of {T}"
%{T%}:т <=> [ :л | :р | \%{и%}: [ :н - %{м%}: ] ] %>: _ ; 

"Realisation of {T} as р"
%{T%}:р  =>                      %{ː%}: %>: _ ; 
            %{ː%}: %>: [ :Vow | :Cns ]+ %>: _   ;
                             Cns %{ъ%}: %>: _ ; 
                                    Vow ( :0) %>: _ ;
                               [ Cns - [ л | р | н ] ] %>: _ ;

!% ҫ ы н {ː} > {Ă} н
!% ҫ ы н  н  0  ӑ  н 

!% ҫ ы н {ː} > {ă} м > {T} {A} н
!% ҫ ы н  н  0  ӑ  м 0  р   а  н

!% п р о с п е к т {ъ} > {T} {A} н
!% п р о с п е к т  0  0  р   а  н

!% п а л ь т о > {T} {A} н
!% п а л ь т о 0  р   а  н

!% п а л ь т о > {ă} м > {T} {A} н
!% п а л ь т о 0  0  м 0  р   а  н


"Realisation of {T} after н"
%{T%}:ч <=> %{и%}: :н %>: _ ; 
               %{м%}:н %>: _ ;

!% п а л ь т о > с е {м} > {T} {A} н
!% п а л ь т о 0 с е  н  0  ч   е  н 

"Realisation of {N} after vowel"
%{N%}:н <=>                     :Vow ( :0) %>: _ ; 
                           е %{м%}:0 %>: _ ; 
!       [ %{ă%}: | %{Ă%}: ] [ м | р ] %>: _  ; 
!                          Cns %{ъ%}: %>: _ ;
                      [ %{и%}:и | %{у%}: ] %{н%}:0 %>: _ ; 

!% п а л ь т о > {и} {н} > {N} {A}
!% п а л ь т 0 0  и   0  0  н   е

!% п а л ь т о > {у} {н} > {N} {A}
!% п а л ь т 0 0  у   0  0  н   а 

!% м е т а л л > {N} {A}
!% м е т а л 0 0  0   а

"Realisation of {N} after possessive"
%{N%}:0 <=> [ %{ă%}: | %{Ă%}: ] р %>: _ %{A%}: ;
                         %{ă%}: м %>: _ %{A%}: ;
                                  

!% п а л ь т о > {ă} м {Ă} р > {N} {A}
!% п а л ь т о 0  0  м  ӑ  р 0  0   а	

!% п р о с п е к т {ъ} > {N} {A}
!% п р о с п е к т  0  0  0   а

"Realisation of {N} after consonant"
%{N%}:0 <=> Cns ([ ь: | Cns:0 | ForceHarmony: ]) %>: _ ;

!% к л а с с > {N} {A}
!% к л а с 0 0  0   а
           
!% с к л а д > {N} {A}
!% с к л а д 0  0   а

!-	пальто<n><px2pl><dat>	пальто>{ă}р>{N}{A}	пальтӑра	пальто<n><px2pl><dat>+?

"Realisation of {н} after vowel"
%{н%}:н <=>   _ %>: %{T%}: ; 

"Realisation of {н} before plural"
%{н%}:0 <=>        [ %{и%}: | %{у%}: ] _ %>: с е %{м%}:  ;
               %>: [ %{и%}: | %{у%}: ] _ .#. ; 
               %>: [ %{и%}: | %{у%}: ] _ %>: [ %{N%}: | ш | п | %{Ă%}: ] ;   ! Dat/Ter/Ins/Gen

!% п а л ь т о > {ă} р > {N} {A}
!% п а л ь т 0 0  ӑ  р 0  0   а

!% п а л ь т о > {и} {н} > {T} {A} н
!% п а л ь т 0 0  и   н  0  ч   е  н

!% п р о с п е к т {ъ} > {ă} м {Ă} р > {N} {A}
!% п р о с п е к т  0  0  ӑ  м  ӑ  р 0  0   а 

"Stem vowel deletion in possessives"
Vx:0 <=> _ ( :0) %>: [ %{у%}: | %{ă%}: р | %{и%}: ]  ;
    where Vx in ( о ) ; 

! {ă} replaces stem -о before р in px2sg
! {ă} is 0 on the surface before м in px1sg

"Back vowel harmony for archiphoneme {ă}"
%{ă%}:ӑ <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ]  | :BackVow ]+ _ ;
      except
              о ( :0) %>: _ м ;

!% ҫ ы н {ː} > {ă} м {Ă} р
!% ҫ ы н  н  0  ӑ  м  ӑ  р 

!% ҫ ы н {ː} > {ă} м
!% ҫ ы н  н  0  ӑ  м  

"Realisation of {ă} after о"
%{ă%}:0 <=> о ( :0) %>: _ м ; 


!% п а л ь т о > {ă} м
!% п а л ь т о 0  0  м

"Consonant doubling reduction in stem"
Cx:0 <=> Cx _ %>: ( :0) [ :Cns | :Vow ] ;
    where Cx in ( с л ) ;

!% к л а с с > {ă} р > {Ă} н
!% к л а с 0 0  ӑ  р 0  ӑ  н 

"Stem vowel assimilation in possessive"
Vx:0 <=> _ ( :0) %>: %{и%}: ; 
      where Vx in ( ӑ ӗ я а е  ) ; 

!% л и т е р а т у р а > {и} {н}
!% л и т е р а т у р 0 0  и   0 


"Vowel reduction before suffix"
Vx:Vy <=> %{й%}: _ %>: \%{и%}: ; 
      where Vx in ( я ) 
            Vy in ( ӑ ) 
      matched ;

!% н я н  {й} я > п {A}	
!% н я н   ь   ӑ 0 п  а


"Consonant gemination in some forms"
%{ː%}:Cx <=> Cx _ ( :0) %>: [ %{ă%}: | %{Ă%}: | %{и%}: ] ;
       where Cx in ( к л т н п в й  ) ; 

!% ч и к {ː} ӗ > {и} {н}
!% ч и к  к  0 0  и   0 
          
"No consonant gemination in certain forms"
%{ː%}:0 <=> _ %>: [ ш | п | %{T%}: | с ] ; 
            _ Vow %>: [ ш | п | %{T%}: | с ] ; 
            _ Vow .#. ; 

!% ҫ ы н {ː} > {и} {н} > с е {м} > ш {Ă} н
!% ҫ ы н  н  0  и   0  0 с е  м  0 ш  ӗ  н 


!% п у л {ː} ӑ > ш {Ă} н
!% п у л  0  ӑ 0 ш  ӑ  н

"Palatalisation of т before possessive"
Cx:ч <=>     _ ( [ %{ъ%}: | ь: ] ) %>: %{и%}: %{н%}:  ;
     except
            _ ( %{ъ%}: ) %>: %{и%}: %{н%}: %>: %{N%}: ; 
     where Cx in ( т  д  ) ;

!% п р о с п е к т {ъ} > {и} {н} > ш {Ă} н
!% п р о с п е к ч  0  0  ӗ   0  0 ш  ӗ  н 

!% п р о с п е к т {ъ} > {и} {н} > {N} {A}
!% п р о с п е к т  0  0  0   0  0  н   е 



!-	проспект<n><px3sp><dat>	проспект{ъ}>{и}{н}>{N}{A}	проспектне	проспект<n><px3sp><dat>+?


!% п у р т {ː} ӑ > {и} {н}
!% п у р т  т  0 0  и   0 


"Realisation of {в}"
%{в%}:в <=> _ %>: %{и%}: ; 

!"Reduction of underlying rounded vowel before {в}"
!Vx:Vy => _ %{в%}:в %>: %{и%}: ;
!     where Vx in ( о у ӳ )
!           Vy in ( ӑ ӑ ӗ ) 
!     matched ; 

!% п ӳ {в} > {и} {н}
!% п ӗ  в  0  ӗ   0

"No reduction of underlying rounded high vowel before {в}"
Vx:Vy => _ %{в%}:в %>: %{и%}: ;
     where Vx in ( у ӳ )
           Vy in ( ӑ ӗ ) 
     matched ; 


"Realisation of {и}"
%{и%}:ӗ <=>                                            т:ч ( %{ъ%}: ) %>: _ %{н%}: ; 
        [ ш | к | р | п | м | л | й | %{й%}: | :ч | ҫ | ь: | н | с | :в ] (Cns:0) %>: _ ; 
      except
          т ь:й е: %>: _ ;        ! платье → платйи

!% п р о с п е к т {ъ} > {и} {н}
!% п р о с п е к ч  0  0  ӗ   0

!% х ӗ в > {и} {н}
!% х ӗ в 0  ӗ   0

!% р а д и о {в} > {и} {н}
!% р а д и о  в  0  ӗ   0 

"Realisation of {и} word final"
%{и%}:и <=>        %>: _ %{н%}: ; 
          т ь:й е: %>: _ ;        ! платье → платйи
        except
            т:ч ( %{ъ%}: ) %>: _ %{н%}: ;
       [ [ ш | к | р | п | м | л | й | %{й%}: | :ч | ҫ | ь: | н | с | :в ] - ь:й ] (Cns:0) %>: _ ; 

!% т и м ӗ р ҫ ӗ > {и} {н}
!% т и м ӗ р ҫ 0 0  и   0

"Soft sign deletion before suffix"
ь:0 <=>     _ %>: ; 
     except 
          т _ ( :0) %>: %{и%}:и   ;

"Soft sign surfaces as i kratka" 
ь:й <=> [ т | с ] _ ( :0) %>: %{и%}:и ;     ! FIXME: Check consonants, are some "soft" by default ?

!% п л а т ь е > {и} {н}
!% п л а т й 0 0  и   0 

!% к ӗ с ь е > {и} {н}
!% к ӗ с й 0 0  и   0 

"Ban soft sign after vowel "
%{й%}:ь /<=  Vow _   ; 

"Surface realisation of jotisation as ь"
%{й%}:ь <=   _  я:ӑ %>: ; 
            н _ (Vow: ) %>:  ;

"Surface realisation of jotisation as й"
%{й%}:й <=> Vow _ ( :0) %>: %{и%}: ; 

"Surface realisation of jotisation as й"
%{й%}:й /<= н  _ ; 

"Does not surface in nominative"
%{й%}:0 <=> _ Vow .#.  ;
         Vow _ .#.  ; 
            
!% с т и х и {й} я > п {A}
!% с т и х и  й  ӑ 0 п  а

!% и н ф о р м а ц и {й} > {и} {н}
!% и н ф о р м а ц и  й  0  ӗ   0 


