Alphabet
  а ӑ е ё ӗ и о у ӳ ы э ю я б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ
  А Ӑ Е Ё Ӗ И О У Ӳ Ы Э Ю Я Б В Г Д Ж З К Л М Н П Р С Ҫ Т Ф Х Ц Ч Ш Щ Й Ь Ъ 
 %{A%}:а %{A%}:е
 %{Ă%}:ӑ %{Ă%}:ӗ %{Ă%}:0
 %{ă%}:ӑ %{ă%}:ӗ
 %{T%}:т %{T%}:р %{T%}:ч
 %{и%}:и %{и%}:ӗ %{и%}:0 
 %{м%}:м %{м%}:0
 %{н%}:н %{н%}:0
 %{N%}:н %{N%}:0
 %{й%}:й %{й%}:0 %{й%}:ь 
 %{у%}:ӳ %{у%}:у
 %{ː%}:0 %{ː%}:н
 %{ъ%}:0 
 %{ь%}:0

 %>:0 
;

Sets

ArchiBack = %{ъ%} ; 

ArchiFront = %{ь%} ; 

BackVow = ӑ а ы о у я ё ю ;

FrontVow = ӗ э и ӳ ; 

Vow = ӑ а ы о у я ё ю ӗ э и ӳ ;

ArchiCns = %{м%} %{н%} %{T%} %{й%} %{ː%} %{N%} ;

Cns = б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ ; 

Rules 

"Back vowel harmony for archiphoneme {A}"
%{A%}:а <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :BackVow ]+ _ ; 

!% п а л ь т о > {N} {A}
!% п а л ь т о 0  н   а

"Back vowel harmony for archiphoneme {Ă}"
%{Ă%}:ӑ <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ] | :BackVow ]+ _ ;
      except
            :ӑ %>: _ ;
             о %>: _ н ;
 %{у%}:у %{н%}: %>: _ н ; 


"Back vowel harmony for archiphoneme {у}"
%{у%}:у <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ]  | :BackVow ]+ _ ;

!% п а л ь т о > {ă} р > ш {Ă} н
!% п а л ь т 0 0  ӑ  р 0 ш  ӑ  н

"Non surface {Ă} in plural genitive"
%{Ă%}:0 <=> %{м%}: %>: _ н ; 
            :ӑ %>: _ ; 
            о %>: _ н ;
            :Vow [ :0 ]* %>: _ н ;

!% п а л ь т о > {Ă} н
!% п а л ь т о 0  0  н

!% п а л ь т о > {и} {н} > {Ă} н
!% п а л ь т 0 0  и   0  0  0  н

!% п а л ь т о > {у} {н} > {Ă} н
!% п а л ь т 0 0  у   0  0  0  н

"Realisation of {м}"
%{м%}:м <=> _ .#. ;           ! Nominative
            _ %>: [ п | ш ] ; ! Instrumental, Terminative

"Realisation of {м} with certain obliques"
%{м%}:н <=> _ %>: %{T%}: ; 

"Realisation of {T}"
%{T%}:т <=> [ :л | :р | \%{и%}: [ :н - %{м%}: ] ] %>: _ ; 

"Realisation of {T} as р"
%{T%}:р  =>                      %{ː%}: %>: _ ; 
            %{ː%}: %>: [ :Vow | :Cns ]+ %>: _   ;
                             Cns %{ъ%}: %>: _ ; 
                                    Vow %>: _ ;
                               [ Cns - [ л | р | н ] ] %>: _ ;

!% ҫ ы н {ː} > {Ă} н
!% ҫ ы н  н  0  ӑ  н 

!% ҫ ы н {ː} > {ă} м > {T} {A} н
!% ҫ ы н  н  0  ӑ  м 0  р   а  н

!% п р о с п е к т {ъ} > {T} {A} н
!% п р о с п е к т  0  0  р   а  н

!% п а л ь т о > {T} {A} н
!% п а л ь т о 0  р   а  н

!% п а л ь т о > {ă} м > {T} {A} н
!% п а л ь т о 0  0  м 0  р   а  н


"Realisation of {T} after н"
%{T%}:ч <=> %{и%}: :н %>: _ ; 
               %{м%}:н %>: _ ;

!% п а л ь т о > с е {м} > {T} {A} н
!% п а л ь т о 0 с е  н  0  ч   е  н 

"Realisation of {N} after vowel"
%{N%}:н <=>                     :Vow %>: _ ;
                           е %{м%}:0 %>: _ ; 
!       [ %{ă%}: | %{Ă%}: ] [ м | р ] %>: _  ; 
                          Cns %{ъ%}: %>: _ ;
                      [ %{и%}:и | %{у%}: ] %{н%}:0 %>: _ ; 

!% п а л ь т о > {и} {н} > {N} {A}
!% п а л ь т 0 0  и   0  0  н   е

!% п а л ь т о > {у} {н} > {N} {A}
!% п а л ь т 0 0  у   0  0  н   а 

"Realisation of {N} after possessive"
%{N%}:0 <=> [ %{ă%}: | %{Ă%}: ] р %>: _ %{A%}: ;
                         %{ă%}: м %>: _ %{A%}: ;
                                  

!% п а л ь т о > {ă} м {Ă} р > {N} {A}
!% п а л ь т о 0  0  м  ӑ  р 0  0   а	


!-	пальто<n><px2pl><dat>	пальто>{ă}р>{N}{A}	пальтӑра	пальто<n><px2pl><dat>+?

"Realisation of {н} after vowel"
%{н%}:н <=>   _ %>: %{T%}: ; 

"Realisation of {н} before plural"
%{н%}:0 <=>        [ %{и%}: | %{у%}: ] _ %>: с е %{м%}:  ;
               %>: [ %{и%}: | %{у%}: ] _ .#. ; 
               %>: [ %{и%}: | %{у%}: ] _ %>: [ %{N%}: | ш | п | %{Ă%}: ] ;   ! Dat/Ter/Ins/Gen

!% п а л ь т о > {ă} р > {N} {A}
!% п а л ь т 0 0  ӑ  р 0  0   а

!% п а л ь т о > {и} {н} > {T} {A} н
!% п а л ь т 0 0  и   н  0  ч   е  н

!% п р о с п е к т {ъ} > {ă} м {Ă} р > {N} {A}
!% п р о с п е к т  0  0  ӑ  м  ӑ  р 0  0   а 

"Stem vowel deletion in possessives"
Vx:0 <=> _ %>: [ %{у%}: | %{ă%}: р | %{и%}: ]  ;
    where Vx in ( о ) ; 

! {ă} replaces stem -о before р in px2sg
! {ă} is 0 on the surface before м in px1sg

"Back vowel harmony for archiphoneme {ă}"
%{ă%}:ӑ <=> [ ArchiBack: | BackVow: ] [ ArchiCns: | Cns: | %>: | [ :0 - %{и%}: ]  | :BackVow ]+ _ ;
      except
              о %>: _ м ;

!% ҫ ы н {ː} > {ă} м {Ă} р
!% ҫ ы н  н  0  ӑ  м  ӑ  р 

!% ҫ ы н {ː} > {ă} м
!% ҫ ы н  н  0  ӑ  м  

"Realisation of {ă} after о"
%{ă%}:0 <=> о %>: _ м ; 


!% п а л ь т о > {ă} м
!% п а л ь т о 0  0  м

"Consonant doubling reduction in stem"
Cx:0 <=> Cx _ %>: [ :Cns | :Vow ] ;
    where Cx in ( с ) ;

!% к л а с с > {ă} р > {Ă} н
!% к л а с 0 0  ӑ  р 0  ӑ  н 

"Vowel reduction before suffix"
Vx:Vy <=> %{й%}: _ %>:  ; 
      where Vx in ( я ) 
            Vy in ( ӑ ) 
      matched ;

!% н я н  {й} я > п {A}	
!% н я н   ь   ӑ 0 п  а

"Surface realisation of jotisation as ь"
%{й%}:ь <=> _  я:ӑ %>: ; 

"Consonant gemination in some forms"
%{ː%}:Cx <=> Cx _ %>: [ %{ă%}: | %{Ă%}: | %{и%}: ] ;
          
"No consonant gemination in certain forms"
%{ː%}:0 <=> _ %>: [ ш | п | %{T%}: | с ] ; 
            _ .#. ; 

!% ҫ ы н {ː} > {и} {н} > с е {м} > ш {Ă} н
!% ҫ ы н  н  0  и   0  0 с е  м  0 ш  ӗ  н 

"Realisation of {и}"
%{и%}:ӗ <=> т:ч ( %{ъ%}: ) %>: _ %{н%}: ; 

!% п р о с п е к т {ъ} > {и} {н}
!% п р о с п е к ч  0  0  ӗ   0

"Realisation of {и} word final"
%{и%}:и <=> %>: _ %{н%}: ; 
        except
            т:ч ( %{ъ%}: ) %>: _ %{н%}: ;

"Palatalisation of т before possessive"
т:ч <=>     _ ( %{ъ%}: ) %>: %{и%}: %{н%}: %>: ;
     except
            _ ( %{ъ%}: ) %>: %{и%}: %{н%}: %>: %{N%}: ; 

!% п р о с п е к т {ъ} > {и} {н} > ш {Ă} н
!% п р о с п е к ч  0  0  ӗ   0  0 ш  ӗ  н 

!% п р о с п е к т {ъ} > {и} {н} > {N} {A}
!% п р о с п е к т  0  0  0   0  0  н   е 



!-	проспект<n><px3sp><dat>	проспект{ъ}>{и}{н}>{N}{A}	проспектне	проспект<n><px3sp><dat>+?


