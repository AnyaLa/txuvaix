Multichar_Symbols

%<n%>                ! Имя существительное
%<np%>               !

%<pl%>               ! 

%<px1sg%>            !
%<px2sg%>            ! 
%<px3sp%>            ! 
%<px1pl%>            ! 
%<px2pl%>            ! 

%<nom%>              !  
%<dat%>              ! 
%<gen%>              ! 
%<loc%>              ! 
%<abl%>              ! 
%<ins%>              ! 
%<ter%>              ! 

%{Ă%}                ! 
%{A%}                !
%{н%}                !
%{й%}                !
%{и%}                !
%{м%}                !
%{T%}                !
%{N%}                !
%{ă%}                !
%{у%}                !
%{ː%}                !
%{ъ%}                !
%{ь%}                !
%{в%}

%>                   !

LEXICON Root

Nouns ;
Proper ;

LEXICON K

# ; 

LEXICON CASE 

%<nom%>:                  K ;
%<gen%>:%>%{Ă%}н          K ;
%<dat%>:%>%{N%}%{A%}      K ; 
%<loc%>:%>%{T%}%{A%}      K ; 
%<abl%>:%>%{T%}%{A%}н     K ; 
%<ins%>:%>п%{A%}          K ;
%<ter%>:%>ш%{Ă%}н         K ; 

LEXICON PLURAL

                          CASE ;
%<pl%>:%>се%{м%}          CASE ; 

LEXICON POSS 

PLURAL ;
%<px1sg%>:%>%{ă%}м        PLURAL ;
%<px2sg%>:%>%{у%}%{н%}    PLURAL ;
%<px3sp%>:%>%{и%}%{н%}    PLURAL ;
%<px1pl%>:%>%{ă%}м%{Ă%}р  PLURAL ;
%<px2pl%>:%>%{ă%}р        PLURAL ;

LEXICON SUBST 

POSS ; 

LEXICON NP

%<np%>: SUBST ;

LEXICON N

%<n%>: SUBST ;

LEXICON POSS-ANNE 

:нне                       PLURAL ;
%<px1sg%>:нн%>ем           PLURAL ;
%<px2sg%>:нн%{ь%}%>%{у%}%{н%}       PLURAL ;
%<px3sp%>:мӑш%>%{и%}%{н%}  PLURAL ;
%<px1pl%>:нне%>м%{Ă%}р     PLURAL ;
%<px2pl%>:нн%>ӗр           PLURAL ;

LEXICON POSS-ATTE 

:тте                       PLURAL ;
%<px1sg%>:тт%>ем           PLURAL ;
%<px2sg%>:ҫ%>%{у%}%{н%}       PLURAL ;
%<px3sp%>:шш%>%{и%}%{н%}  PLURAL ;
%<px1pl%>:тте%>м%{Ă%}р     PLURAL ;
%<px2pl%>:ҫ%>ӑр           PLURAL ;

LEXICON POSS-ATTEANNE

:тте-анне                  PLURAL ;
%<px1sg%>:тте-анн%>ем           PLURAL ;
%<px2sg%>:ҫу-анн%{ь%}%>%{у%}%{н%}       PLURAL ;
%<px3sp%>:шӗ-амӑш%>%{и%}%{н%}  PLURAL ;
%<px1pl%>:тте-анне%>м%{Ă%}р     PLURAL ;
%<px2pl%>:ҫӑр-анн%>ӗр           PLURAL ;

LEXICON N-ANNE 

%<n%>: POSS-ANNE ; 

LEXICON N-ATTE 

%<n%>: POSS-ATTE ; 

LEXICON N-ATTEANNE

%<n%>: POSS-ATTEANNE ; 

LEXICON N-VLAÇ

%<n%>:ç SUBST ;
%<n%>%<nom%>:сть # "weight: 1.0" ; 

LEXICON Nouns 

анне:а N-ANNE ; ! ""
атте:а N-ATTE ; ! ""
атте-анне:а N-ATTEANNE ; ! ""

власть:вла N-VLAÇ ; ! ""

меню:меню%{в%} N ; ! ""
пӳ:пӳ%{в%} N ; ! ""
радио:радио%{в%} N ; ! ""
ту:ту%{в%} N ; ! ""
хӑю:хӑю%{в%} N ; ! ""
кино:кино%{в%} N ; ! ""

шӑши:шӑши%{й%} N ; ! ""
ҫурт:ҫурт N ; ! ""

автан:автан N ; ! ""
автобус:автобус N ; ! ""
аллея:алле%{й%}я N ; ! ""


барокко:барокко N ; ! ""
вӑйӑ:вӑй%{ː%}ӑ N ; ! ""
вӑкӑр:вӑкӑр N ; ! ""
герой:герой N ; ! ""
звено:звено N ; ! ""
информаци:информаци%{й%} N ; ! ""
июль:июль N ; ! ""
июнь:июнь N ; ! ""
йӗп:йӗп%{ː%} N ; ! ""
каҫма:каҫма N ; ! ""
кенгуру:кенгуру N ; ! ""
класс:класс N ; ! ""
контролёр:контролёр N ; ! ""
кофе:кофе N ; ! ""
кролик:кролик N ; ! ""
кукӑль:кукӑль N ; ! ""
кӗнеке:кӗнеке N ; ! ""
кӗсье:кӗсье N ; ! ""
кӳлӗ:кӳл%{ː%}ӗ N ; ! ""
лаша:лаша N ; ! ""
литература:литература N ; ! ""
май:май N ; ! ""
металл:металл N ; ! ""
министерство:министерство N ; ! ""
музей:музей N ; ! ""
няня:нян%{й%}я N ; ! ""
пальто:пальто N ; ! ""
платье:платье N ; ! ""
проспект:проспект%{ъ%} N ; ! ""
пулӑ:пул%{ː%}ӑ N ; ! ""
пуртӑ:пурт%{ː%}ӑ N ; ! ""
склад:склад N ; ! ""
сарай:сарай N ; ! ""
тетрадь:тетрадь N ; ! ""
стена:стена N ; ! ""
стихия:стихи%{й%}я N ; ! ""
супӑнь:супӑнь N ; ! ""
сӑвӑ:сӑв%{ː%}ӑ N ; ! ""
тенкӗ:тенкӗ N ; ! ""
тимӗрҫӗ:тимӗрҫӗ N ; ! ""
тухья:тухь%{й%}я N ; ! ""
уй:уй N ; ! ""
урам:урам N ; ! ""
утӑ:утӑ N ; ! ""
училище:училище N ; ! ""
хурҫӑ:хурҫӑ N ; ! ""
хӗв:хӗв N ; ! ""
хӗвел:хӗвел N ; ! ""
чей:чей N ; ! ""
чикӗ:чик%{ː%}ӗ N ; ! ""
чӗрӗп:чӗрӗп N ; ! ""
шоссе:шоссе N ; ! ""
ҫын:ҫын%{ː%} N ; ! ""
ӗҫ:ӗҫ N ; ! ""


LEXICON Proper


Женя:Жен%{й%}я NP ; ! ""
Золотницкий:Золотницкий NP ; ! ""
Иванова:Иванова NP ; ! ""
Наталья:Наталь%{й%}я NP ; ! ""
Саша:Саша NP ; ! ""
Турку:Турку NP ; ! ""


