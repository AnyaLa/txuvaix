Multichar_Symbols

%<n%>                                ! Имя существительное
%<np%>                               !

%<pl%>                               ! Множественное число

%<px1sg%>                            !
%<px2sg%>                            ! 
%<px3sp%>                            ! 
%<px1pl%>                            ! 
%<px2pl%>                            ! 

%<nom%>                              ! Именительный падеж
%<dat%>                              ! Дательный падеж
%<gen%>                              ! Родительный падеж
%<loc%>                              ! Местный падеж
%<abl%>                              ! Исходный падеж
%<ins%>                              ! Творительный падеж
%<ter%>                              ! 

%{Ă%}                                ! ӑ / ӗ / 0
%{Ĕ%}                                ! ӑ / ӗ / 0
%{A%}                                ! а / е / 0
%{н%}                                ! н / 0
%{й%}                                ! й / 0
%{и%}                                ! и / ӗ / 0
%{м%}                                ! м / н 
%{T%}                                ! т / р / ч
%{N%}                                ! н / 0
%{ă%}                                ! ӑ / ӗ / 0
%{U%}                                ! у / ӳ / 0
%{в%}                                ! в / 0
%{а%}                                ! а / ӑ / 0
%{о%}                                ! о / ӑ / 0
%{е%}                                ! е / ӗ / 0

%{ː%}                                ! 
%{ъ%}                                ! 
%{ь%}                                !
%{☭%}                                ! 

%>                                   ! Граница морфем

LEXICON Root

Nouns ;
Proper ;

LEXICON K

# ; 

LEXICON CASE 

%<nom%>:                             K ;
%<gen%>:%>%{Ă%}н                     K ;
%<dat%>:%>%{N%}%{A%}                 K ; 
%<loc%>:%>%{T%}%{A%}                 K ; 
%<abl%>:%>%{T%}%{A%}н                K ; 
%<ins%>:%>п%{A%}                     K ;
%<ter%>:%>ш%{Ă%}н                    K ; 

LEXICON PLURAL

                                     CASE ;
%<pl%>:%>се%{м%}                     CASE ; 

LEXICON POSS 

                                     PLURAL ;
%<px1sg%>:%>%{ă%}м                   PLURAL ;
%<px2sg%>:%>%{U%}%{н%}               PLURAL ;
%<px3sp%>:%>%{и%}%{н%}               PLURAL ;
%<px1pl%>:%>%{ă%}м%{Ă%}р             PLURAL ;
%<px2pl%>:%>%{Ĕ%}р                   PLURAL ;

LEXICON SUBST 

POSS ; 

LEXICON NP

%<np%>:                              SUBST ;

LEXICON N

%<n%>:                               SUBST ;

LEXICON POSS-ANNE 

:нне                                 PLURAL ;
%<px1sg%>:нн%>ем                     PLURAL ;
%<px2sg%>:нн%{ь%}%>%{U%}%{н%}        PLURAL ;
%<px3sp%>:мӑш%>%{и%}%{н%}            PLURAL ;
%<px1pl%>:нне%>м%{Ă%}р               PLURAL ;
%<px2pl%>:нн%>ӗр                     PLURAL ;

LEXICON POSS-ATTE 

:тте                                 PLURAL ;
%<px1sg%>:тт%>ем                     PLURAL ;
%<px2sg%>:ҫ%>%{U%}%{н%}              PLURAL ;
%<px3sp%>:шш%>%{и%}%{н%}             PLURAL ;
%<px1pl%>:тте%>м%{Ă%}р               PLURAL ;
%<px2pl%>:ҫ%>ӑр                      PLURAL ;

LEXICON POSS-ATTEANNE

:тте-анне                            PLURAL ;
%<px1sg%>:тте-анн%>ем                PLURAL ;
%<px2sg%>:ҫу-анн%{ь%}%>%{U%}%{н%}    PLURAL ;
%<px3sp%>:шӗ-амӑш%>%{и%}%{н%}        PLURAL ;
%<px1pl%>:тте-анне%>м%{Ă%}р          PLURAL ;
%<px2pl%>:ҫӑр-анн%>ӗр                PLURAL ;

LEXICON N-ANNE 

%<n%>:                               POSS-ANNE ; 

LEXICON N-ATTE 

%<n%>:                               POSS-ATTE ; 

LEXICON N-ATTEANNE

%<n%>:                               POSS-ATTEANNE ; 

LEXICON N-VLAÇ

%<n%>:ҫ                              SUBST ;
%<n%>%<nom%>:сть                     # "weight: 1.0" ; 
%<n%>%<px2sg%>%<dat%>:ҫ%>н%{A%}      K "weight: 1.0" ; 

LEXICON N-SUPĂN

%<n%>:                               SUBST ;
%<n%>%<loc%>:%>р%{A%}                K "weight: 1.0" ; 
%<n%>%<abl%>:%>р%{A%}н               K "weight: 1.0" ; 
%<n%>%<px2sg%>%<dat%>:%>н%{A%}       K "weight: 1.0" ; 

LEXICON N-RADIO

%<n%>:%{в%}%{☭%}                     SUBST ;

LEXICON N-TU

%<n%>:%{в%}                          SUBST ;
%<n%>%<px2sg%>%<dat%>:%{в%}%>н%{A%}  K "weight: 1.0" ; 

LEXICON N-ĔŞ 

%<n%>:                               SUBST ;
%<n%>%<px2sg%>%<dat%>:%>н%{A%}       K "weight: 1.0" ; 

LEXICON NP-ZOLOTNICKIJ

%<np%>:%{й%}                         SUBST ;
%<np%>%<nom%>:й                      K "weight: -1.0" ;


LEXICON Nouns 

анне:а                               N-ANNE ; ! ""
атте:а                               N-ATTE ; ! ""
атте-анне:а                          N-ATTEANNE ; ! ""

власть:вла                           N-VLAÇ ; ! ""

меню:мен%{й%}ю                       N-RADIO ; ! ""
радио:радио                          N-RADIO ; ! ""
кино:кино                            N-RADIO ; ! ""
кенгуру:кенгуру                      N-RADIO ; ! ""

пӳ:пӳ                                N-TU ; ! ""
ту:ту                                N-TU ; ! ""
хӑю:хӑ%{й%}ю                         N-TU ; ! ""

шӑши:шӑши%{й%}                       N-ĔŞ ; ! ""
ҫурт:ҫурт                            N-ĔŞ ; ! ""

автан:автан                          N-SUPĂN ; ! ""
автобус:автобус                      N-ĔŞ ; ! ""
аллея:алле%{й%}я                     N ; ! ""
барокко:барокк%{о%}                  N ; ! ""
вӑйӑ:вӑй%{ː%}ӑ                       N ; ! ""
вӑкӑр:вӑкӑр                          N-ĔŞ ; ! ""
герой:герой                          N-ĔŞ ; ! ""
звено:звено                          N ; ! ""
информаци:информаци%{й%}             N-ĔŞ ; ! ""
июль:июль                            N-SUPĂN ; ! ""
июнь:июнь                            N-SUPĂN ; ! ""
йӗп:йӗп%{ː%}                         N ; ! ""
каҫма:каҫма                          N ; ! ""
класс:класс                          N-ĔŞ ; ! ""
контролёр:контролёр                  N-ĔŞ ; ! ""
кофе:коф%{е%}                        N ; ! ""
кролик:кролик%{ъ%}                   N-ĔŞ ; ! ""
кукӑль:кукӑль                        N-SUPĂN ; ! ""
кӗнеке:кӗнеке                        N ; ! ""
кӗсье:кӗсье                          N ; ! ""
кӳлӗ:кӳл%{ː%}ӗ                       N ; ! ""
лаша:лаша                            N ; ! ""
литература:литератур%{а%}            N ; ! ""
май:май                              N-ĔŞ ; ! ""
металл:металл                        N-ĔŞ ; ! ""
министерство:министерств%{о%}        N ; ! ""
музей:музей                          N ; ! ""
музей:музей%{ъ%}                     N "weight: 1.0" ; ! ""
няня:нян%{й%}я                       N ; ! ""
пальто:пальто                        N ; ! ""
платье:платье                        N ; ! ""
!платье:плать%{е%}                    N ; ! ""
проспект:проспект%{ъ%}               N-ĔŞ ; ! ""
пулӑ:пул%{ː%}ӑ                       N ; ! ""
пуртӑ:пурт%{ː%}ӑ                     N ; ! ""
склад:склад                          N-ĔŞ ; ! ""
сарай:сарай                          N-ĔŞ ; ! ""
тетрадь:тетрадь                      N-ĔŞ ; ! ""
стена:стена                          N ; ! ""
стихия:стихи%{й%}я                   N ; ! ""
супӑнь:супӑнь                        N-SUPĂN ; ! ""
сӑвӑ:сӑв%{ː%}ӑ                       N ; ! ""
тенкӗ:тенкӗ                          N ; ! ""
тимӗрҫӗ:тимӗрҫӗ                      N ; ! ""
тухья:тухь%{й%}я                     N ; ! ""
уй:уй                                N-ĔŞ ; ! ""
урам:урам                            N-ĔŞ ; ! ""
утӑ:ут%{ː%}ӑ                         N ; ! ""
училище:училищ%{е%}                  N ; ! ""
хурҫӑ:хурҫӑ                          N ; ! ""
хӗв:хӗв                              N-ĔŞ ; ! ""
хӗвел:хӗвел                          N-ĔŞ ; ! ""
!чей:чей                              N-ĔŞ ; ! ""
чей:чей                              N ; ! ""
чикӗ:чик%{ː%}ӗ                       N ; ! ""
чӗрӗп:чӗрӗп                          N-ĔŞ ; ! ""
шоссе:шоссе                          N ; ! ""
ҫын:ҫын%{ː%}                         N ; ! ""
ӗҫ:ӗҫ                                N-ĔŞ ; ! ""


LEXICON Proper


Женя:Жен%{й%}я                       NP ; ! ""
Золотницкий:Золотницки               NP-ZOLOTNICKIJ ; ! ""
Иванова:Иванов%{а%}                  NP ; ! ""
Наталья:Наталь%{й%}я                 NP ; ! ""
Саша:Саш%{а%}                        NP ; ! ""
Турку:Турку                          NP ; ! ""


